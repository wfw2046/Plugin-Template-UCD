/* The Groovy plugin for Gradle assumes the following layout
 * src/main/java
 * src/main/resources
 * src/main/groovy
 *
 * src/test/java
 * src/test/resources
 * src/test/groovy
 *
 * src/<sourceSet>/java
 * src/<sourceSet>/resources
 * src/<sourceSet>/groovy
 *
 * You can override or create a new sourceSet with the following snippet:
 *
 * sourceSets {
 *     main {
 *         // We can override src/main/groovy to also add src/main/zip
 *         groovy {
 *             srcDirs = ['src/main/groovy', 'src/main/zip']
 *         }
 *     }
 *     mySourceSet {
 *         groovy {
 *             srcDirs = ['src/myDir/groovy']
 *         }
 *     }
 * }
 */
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'groovy'

defaultTasks 'distPlugin'

def buildLocaleDir = 'build/locale'

configurations {
    // Remove the groovy-all jar from runtime dependencies
    runtime.exclude module: 'groovy-all'
}

repositories {
    mavenCentral()
    maven {
        url "https://public.dhe.ibm.com/software/products/UrbanCode/maven2/"
    }
}

dependencies {
    // groovy-plugin-utils pulls down groovy-all as a transitive dependency for the 'compile' configuration
    compile 'com.ibm.urbancode.plugins:groovy-plugin-utils:1.0'
    compile 'com.ibm.urbancode.util:i18n-scraper:+'
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.6'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
    compile group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.7.1'
    compile group: 'commons-logging', name: 'commons-logging', version: '1.2'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
    compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.2.6'
    compile group: 'xml-resolver', name: 'xml-resolver', version: '1.2'
    runtime files('libs/json-lib-2.4-jdk15.jar')
    
    // This compiles the i18n-scraper script
    compile localGroovy()
//    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.3.6'
//    testCompile group: 'junit', name: 'junit', version: '4.11'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/groovy', 'src/main/zip', 'src/main/util']
        }
    }
}

task copyDeps(type: Copy) {
    from configurations.runtime
    into 'lib'
}

task distPlugin(type: Zip, dependsOn: ['compileGroovy', 'gatherI18n']) {
    def releaseVersion = apiVersion + "." + pluginVersion
    from(sourceSets.main.groovy.srcDirs) {
        filter(ReplaceTokens, tokens: [API_VERSION: apiVersion, RELEASE_VERSION: releaseVersion])
    }
    into('lib') {
        from copyDeps
    }
    into('locale') {
        from buildLocaleDir
    }
    archiveName = "${pluginName}-${releaseVersion}.zip"
}

task gatherI18n(type: JavaExec) {
    delete buildLocaleDir
    mkdir buildLocaleDir

    main = 'i18n-scraper'
    args 'build/locale/en.properties'
    classpath = sourceSets.main.runtimeClasspath
}

task cleanAll(dependsOn: ['clean', 'cleanCopyDeps', 'cleanDistPlugin', 'cleanGatherI18n'])
